plugins {
    id "org.wisepersist.gwt" version "1.1.18"
}
apply plugin: "java"
apply plugin: "maven-publish"
if(project.gradle.startParameter.taskNames.contains("gwtDev")) {
    apply plugin: "war"
    apply plugin: "gwt"
    gwt.modules "net.sayaya.Test"
    sourceSets.main.java.srcDirs += 'src/test/java'
} else {
    apply plugin: "gwt-base"
    publishing {
        publications {
            maven(MavenPublication) {
                groupId 'net.sayaya'
                artifactId 'ui'
                version "4.0"
                from components.java
            }
        }
    }
}

group "net.sayaya"
version "4.0"
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {
    implementation('org.jboss.elemento:elemento-core:1.0.10-gwtcom') {
        exclude group:"com.google.gwt", module:"gwt-user"
    }
    implementation("com.google.elemental2:elemental2-svg:1.1.0")
    implementation("org.gwtproject:gwt-dev:2.10.0")
    implementation("org.gwtproject:gwt-user:2.10.0")
    implementation("org.projectlombok:lombok:1.18.24")
    annotationProcessor("org.projectlombok:lombok:1.18.24")
    testCompileOnly("org.projectlombok:lombok:1.18.24")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.24")

    implementation("javax.validation:validation-api:2.0.1.Final")
}

ext {
    lombok = project.configurations.annotationProcessor.find { it.name.startsWith("lombok") }
}
if(project.gradle.startParameter.taskNames.contains("gwtDev")) {
    gwtDev {
        jvmArgs "-XX:ReservedCodeCacheSize=512M","-javaagent:${lombok}=ECJ"
        port 8888
        war file("src/test/webapp")
    }
} else {
    jar {
        from sourceSets.main.allSource
        archiveName "${archivesBaseName}.jar"
    }
}
compileGwt {
    minHeapSize = "2048M"
    maxHeapSize = "4096M"
    jvmArgs "-XX:ReservedCodeCacheSize=512M","-Xss2M","-javaagent:${lombok}=ECJ"
}